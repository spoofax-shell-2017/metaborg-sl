module trans/semantics/functions

imports
  trans/semantics/values

/*
* Function definitions
*/
signature
  arrows
    List(FunDef) -load-> U
    FunDef -load-> U
    FunDef -fun2val-> V

rules

  fs : List(FunDef) -load-> U()
  where
    case fs of {
      [f | fs'] =>
        f -load-> _;
        fs' -load-> _
      [] =>
    }
    
  FunDef(name, params, body) :: GE, E -load-> U() :: GE {entry, GE}, E {entry, E}
  where
    {name |--> FunV(name, params, body)} => entry

rules

  FunCall(name, args) --> v
  where
    lookup(name) --> FunV(_, params, body);
    args --> vargs;
    invoke(vargs, params, body) --> v

/*
* Function calls
*/
signature
  arrows
    invoke(List(V), List(String), Stmt) --> V
    bind(List(V), List(String)) --> E
  
  sorts
    ReturnHandler
  
  constructors
    doReturn : V -> ReturnHandler // will throw a ReturnException with the value and the semantic components
    onReturn : Stmt -> ReturnHandler // will catch a ReturnException with the value and the semantic components
  
  arrows
    ReturnHandler :: E, GE --> V :: E, GE // natively implemented
  
  arrows
    List(Expr) --> List(V)

  arrows
    bind_aux(List(V), List(String), E) --> E
    
rules  

  invoke(vseq, parseq, stmt) :: E --> v :: E
  where
    bind(vseq, parseq) :: E --> E' :: E _;
    onReturn(stmt) :: E' --> v :: E _
  
  es : List(Expr) --> vs
  where
    case es of {
      [e | es'] =>
        e --> v;
        es' --> vs';
        [v | vs'] => vs
      [] =>
        [] => vs
    }
  
  bind_aux(vs, params, Ecall) --> Ecall'
  where
    case vs of {
      [v | vs'] =>
        params => [p | ps'];
        bind_aux(vs', ps', {p |--> v, Ecall}) --> Ecall'
    otherwise =>
      Ecall => Ecall'
    }
    
  bind(vs, params) :: GE, E --> Ecall :: GE, E
  where
    bind_aux(vs, params, GE) --> Ecall
    
  
  ReturnVoid() --> U()
  where
    doReturn(NullV()) --> _

  Return(v) --> U()
  where
    doReturn(v) --> _



